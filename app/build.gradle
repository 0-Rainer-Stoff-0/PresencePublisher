plugins {
    id 'com.jaredsburrows.license' version '0.8.42'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    id 'org.ajoberstar.grgit' version '4.0.0-rc.1'
}

apply plugin: 'com.android.application'

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    branchVersionIncrementer = [
        'feature/.*' : 'incrementMinor',
        'master' : 'incrementMinor',
    ]
}

def getBuildVersionCode() {
    return grgit.tag.list().findAll{ it.getName().matches('v[1-9].*') }.size()
}

def getBuildApplicationId() {
    def isTagged = grgit.describe(tags: true, match: ['v[1-9]*']) == "v${scmVersion.version}"
    return isTagged ? 'org.ostrya.presencepublisher' : 'org.ostrya.presencepublisher.snapshot'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId getBuildApplicationId()
        minSdkVersion 14
        targetSdkVersion 28
        versionCode getBuildVersionCode()
        versionName scmVersion.version
    }
    signingConfigs {
        release {
            storeFile file(keystorePath)
            storePassword keystorePassword
            keyAlias signkeyAlias
            keyPassword signkeyPassword
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.3'
}

afterEvaluate {
    preBuild.dependsOn(licenseDebugReport)
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.security:security-crypto:1.0.0-alpha02'
    implementation('com.hypertrack:hyperlog:0.0.10') {
        exclude group: 'com.android.support'
        exclude group: 'com.android.volley'
    }
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
}
